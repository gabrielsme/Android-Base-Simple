plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

def apikeyPropertiesFile = rootProject.file("secret.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdk globalConfiguration["compileSdk"]

    defaultConfig {
        applicationId "digital.heylab.androidbasesimple"
        minSdk globalConfiguration["minSdk"]
        targetSdk globalConfiguration["targetSdk"]
        versionCode globalConfiguration["versionCode"]
        versionName globalConfiguration["versionName"]

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "OMDB_KEY", apikeyProperties['OMDB_KEY'])
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.1.1'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    def dependencies = rootProject.ext.dependencies
    def testDependencies = rootProject.ext.testDependencies

    implementation dependencies.coreKtx
    implementation dependencies.appCompat
    implementation dependencies.materialDesign
    implementation dependencies.constraintLayout
    implementation dependencies.activityKtx
    implementation dependencies.composeUi
    implementation dependencies.composeActivity
    implementation dependencies.composeMaterial
    implementation dependencies.composeToolingPreview

    implementation dependencies.hiltAndroid
    kapt dependencies.hiltCompiler

    implementation dependencies.retrofit
    implementation dependencies.retrofitConverterGson
    implementation dependencies.stetho
    implementation dependencies.stethoOkHttp3

    testImplementation testDependencies.junit

    androidTestImplementation testDependencies.extJunit
    androidTestImplementation testDependencies.espresso
    androidTestImplementation testDependencies.composeJunit

    debugImplementation testDependencies.composeTooling
    debugImplementation testDependencies.composeTestManifest
}

kapt {
    correctErrorTypes true
}